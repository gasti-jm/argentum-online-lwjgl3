plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.aoclient'
version = '1.0-SNAPSHOT'

application {
    mainClass = 'org.aoclient.Main'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(19)
    }
}

import org.gradle.internal.os.OperatingSystem

project.ext.lwjglVersion = "3.3.3"
project.ext.jomlVersion = "1.10.5"
project.ext.imguiVersion = "1.86.11"
project.ext.loggerVersion = "2.7.0"

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        project.ext.imguiNatives = "imgui-java-natives-linux"

        def osArch = System.getProperty("os.arch")
        if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
            project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
        } else if (osArch.startsWith("ppc")) {
            project.ext.lwjglNatives += "-ppc64le"
        } else if (osArch.startsWith("riscv")) {
            project.ext.lwjglNatives += "-riscv64"
        }
        break

    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives =
                System.getProperty("os.arch").startsWith("aarch64")
                        ? "natives-macos-arm64"
                        : "natives-macos"
        project.ext.imguiNatives = "imgui-java-natives-macos"
        break

    case OperatingSystem.WINDOWS:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives =
                osArch.contains("64")
                        ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                        : "natives-windows-x86"
        project.ext.imguiNatives = "imgui-java-natives-windows"
        break
}

repositories {
    mavenCentral()
}

dependencies {
    // Logger
    implementation "org.tinylog:tinylog-api:$loggerVersion"
    implementation "org.tinylog:tinylog-impl:$loggerVersion"

    // ImGui
    implementation "io.github.spair:imgui-java-app:$imguiVersion"
    implementation "io.github.spair:imgui-java-binding:$imguiVersion"
    implementation "io.github.spair:imgui-java-lwjgl3:$imguiVersion"
    implementation "io.github.spair:$imguiNatives:$imguiVersion"

    // LWJGL
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-bgfx"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-nanovg"
    implementation "org.lwjgl:lwjgl-nuklear"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-par"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-vulkan"

    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-bgfx::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-nanovg::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-nuklear::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-par::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"

    implementation "org.joml:joml:$jomlVersion"

    if (lwjglNatives == "natives-macos" || lwjglNatives == "natives-macos-arm64") {
        runtimeOnly "org.lwjgl:lwjgl-vulkan::$lwjglNatives"
    }
}

test {
    useJUnitPlatform()
}

